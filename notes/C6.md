# C6. PBR

[TOC]

## 1. 理论

PBR 全称为基于物理的渲染 Physically Based Rendering，要求满足三个条件

- 基于微平面（microfacet）的表面模型
- 能量守恒（energy conserving）
- 基于物理的 BRDF

这个教程着重于 Disney 提出并被 Epic Games 应用于实时渲染的 PBR 方案。

![img](https://learnopengl-cn.github.io/img/07/01/preview.png)

### 微平面模型

### 能量守恒

### 反射方程

### BRDF

实时渲染管线中最广泛使用的模型 Cook-Torrance BRDF
$$
f _ { r } = k _ { d } f _ { \text {lambert} } + k _ { s } f _ { \text {cook-torrance} }
$$

> 这里有误，这里并不需要乘以 $k_s$ 
>
> $k_s$ 是菲涅尔系数，而这已经包含在了 $f_\text{cook-torramce}$ 里边了
>
> 所以实现时公式要改为
> $$
> f _ { r } = k _ { d } f _ { \text {lambert} } + f _ { \text {cook-torrance} }
> $$

其中漫反射部分为
$$
f _ { \text {lambert} } = \frac { c } { \pi }
$$
镜面部分为
$$
f _ { \text {cook-torrance} } = \frac { D F G } { 4 \left( \omega _ { o } \cdot n \right) \left( \omega _ { i } \cdot n \right) }
$$
其中

- D 是法线分布函数（Normal **D**istribution Function）
- F 是菲涅尔方程（**F**resnel Equation）
- G 是几何函数（**G**eometry Function）

#### 法线分布函数

Trowbridge-Reitz GGX [^1] 
$$
N D F _ { G G X T R } ( n , h , \alpha ) = \frac { \alpha ^ { 2 } } { \pi \left( ( n \cdot h ) ^ { 2 } \left( \alpha ^ { 2 } - 1 \right) + 1 \right) ^ { 2 } }
$$

#### 几何函数

GGX 有自己对应的几何函数 [^1]，这里使用的是 GGX 和 Schlick-Beckmann[^2] 近似的结合体，又称为 Schlick-GGX
$$
G _ { S c h l i c k G G X } ( n , v , k ) = \frac { n \cdot v } { ( n \cdot v ) ( 1 - k ) + k }
$$
其中 k 为
$$
\begin{aligned}
k _ { \text {direct} } &= \frac { ( \alpha + 1 ) ^ { 2 } } { 8 } \\
k _ { I B L } &= \frac { \alpha ^ { 2 } } { 2 }\\
\end{aligned}
$$

> Schlick-Beckmann 模型中用的几何函数是 beckmann，所以给出的用于 Smith method 的 $G_1$ 是基于 beckmann 的。现在改为 GGX，因此相应的也要修改这个 k[^1]。
>
> IBL 全称为 **I**maged-**B**ased **L**ighting。
>
> 在预计算 BRDF 时使用 $k_\text{IBL}$，在实时渲染时我们为那些解析光源（点光源，方向光，spotlight）用 $k_\text{direct}$。

使用史密斯法（Smith’s method）结合几何遮蔽和几何阴影

![img](https://learnopengl-cn.github.io/img/07/01/geometry_shadowing.png)
$$
G ( n , v , l , k ) = G _ { s u b } ( n , v , k ) G _ { s u b } ( n , l , k )
$$
菲涅尔方程

菲涅尔方程描述的是光线的反射率。方程很复杂，但可以用 Fresnel-Schlick 近似
$$
F _ { \text { Fchlick } } \left( h , v , F _ { 0 } \right) = F _ { 0 } + \left( 1 - F _ { 0 } \right) ( 1 - ( h \cdot v ) ) ^ { 5 }
$$
$F_0$ 是基础反射率，可以用折射率计算得到。

该近似有一个问题是只能用于电介质，对于金属，我们需要使用一种不同的方程，这样很不方便。

对于电介质，基础反射率都不会高于 0.17，而导体基础反射率较高，大多在 0.5 和 1.0 之间变化，而且一般是带有色彩的（即不同波长反射率不同），这也是 $F_0$ 要用 RGB 表示的原因。

电介质和金属的这种差异就引出了金属工作流的概念，用一个称为金属度（metalness）的参数来描述材质是金属还是非金属。

> 理论上说，金属度是二元的。但大多数渲染管线允许在 0 到 1 之间线性的调配金属度

为了模拟以上行为，可以对 $F_0$ 根据 metallic 调整

```c++
vec3 F0 = vec3(0.04);
F0      = mix(F0, surfaceColor.rgb, metalness);
```

这里为大多数电介质表面定义了一个近似的基础反射率 0.04。

#### Cook-Torrance 反射方程

$$
L _ { o } \left( p , \omega _ { o } \right) = \int _ { \Omega } \left( k _ { d } \frac { c } { \pi } + \frac { D F G } { 4 \left( \omega _ { o } \cdot n \right) \left( \omega _ { i } \cdot n \right) } \right) L _ { i } \left( p , \omega _ { i } \right) n \cdot \omega _ { i } d \omega _ { i }
$$

> 这里我把原文中的 $k_s$ 去掉了

## 2. 光照

## 3. IBL

IBL 全称为 image based lighting，通过周围环境来作为光源。这通常用一个立方体贴图来表示。

我们需要计算反射方程。我们要解决两个问题

- 获取 $L_i(p,\omega_i)$ 
- 实时计算积分

第一个问题可以用环境贴图的 cubemap 解决

```c++
vec3 radiance = texture(cubemapEnvironment, w_i).rgb;  
```

第二个问题是积分。为了解决这个问题，我们会进行预计算或预处理。

我们可以将积分方程拆成两部分
$$
L _ { o } \left( p , \omega _ { o } \right) = \int _ { \Omega } \left( k _ { d } \frac { c } { \pi } \right) L _ { i } \left( p , \omega _ { i } \right) n \cdot \omega _ { i } d \omega _ { i } + \int _ { \Omega }  \frac { D F G } { 4 \left( \omega _ { o } \cdot n \right) \left( \omega _ { i } \cdot n \right) } L _ { i } \left( p , \omega _ { i } \right) n \cdot \omega _ { i } d \omega _ { i }
$$
左边是漫反射部分，右边是高光部分

### 3.1 Diffuse Irradiance

#### 理论

漫反射部分可以写为
$$
L _ { o } \left( p , \omega _ { o } \right) = k _ { d } \frac { c } { \pi } \int _ { \Omega } L _ { i } \left( p , \omega _ { i } \right) n \cdot \omega _ { i } d \omega _ { i }
$$
这个积分只依赖于 $\omega_i$（假设 $p$ 在环境贴图的中间）。因此我们可以对此进行预计算。我们对每一个 $\omega_o$ 样本，我们在相应的半球面上采样大量的 $\omega_i$。

![Convoluting a cubemap on a hemisphere for a PBR irradiance map.](https://learnopengl.com/img/pbr/ibl_hemisphere_sample.png)

> 这个图也是错的，我们应该是根据法线所在的半球进行积分，而不是 $\omega_o$。

这样我们就能得到 irradiance map。

![The effect of convoluting a cubemap environment map.](https://learnopengl.com/img/pbr/ibl_irradiance.png)

> 我们假设 $p$ 在环境贴图的中心。这可能会不符合现实（特别是室内情况）。渲染引擎通过使用反射探头（reflection probes）来解决这个问题，每个反射探头会计算它周围环境的 irradiance map。这样 $p$ 的 irrdiance 可以用各反射探头的插值结果来得到。

#### HDR 文件格式

HDR 文件存储了一个完成的 cubemap，数据可以超出 1.0。

示例

![Example of an equirectangular map](https://learnopengl.com/img/pbr/ibl_hdr_radiance.png)

这是球形环境贴图映射到平面上的结果，称为 equirectangular map。

#### HDR 载入

stb_image.h 支持载入 hdr 文件。示例代码

```c++
stbi_set_flip_vertically_on_load(true);
int width, height, nrComponents;
float *data = stbi_loadf("newport_loft.hdr", &width, &height, &nrComponents, 0);
```

#### 从 Equirectangular 到 Cubemap

直接用 equirectangular 也是可以的，但耗时较多。因此首先将其转化为立方体贴图。

我们可以通过渲染一个立方体来转化贴图。

具体来说，相机朝六个方向渲染六次，每次朝一个方向渲染（只会有一个面在相机视锥体内）。矩阵设定如下

```c++
mat4 captureProjection = perspective(radians(90.0f), 1.0f, 0.1f, 10.0f);
mat4 captureViews[] =
{
    lookAt(vec3(0.0f, 0.0f, 0.0f), vec3( 1.0f,  0.0f,  0.0f), vec3(0.0f, -1.0f,  0.0f)),
    lookAt(vec3(0.0f, 0.0f, 0.0f), vec3(-1.0f,  0.0f,  0.0f), vec3(0.0f, -1.0f,  0.0f)),
    lookAt(vec3(0.0f, 0.0f, 0.0f), vec3( 0.0f,  1.0f,  0.0f), vec3(0.0f,  0.0f,  1.0f)),
    lookAt(vec3(0.0f, 0.0f, 0.0f), vec3( 0.0f, -1.0f,  0.0f), vec3(0.0f,  0.0f, -1.0f)),
    lookAt(vec3(0.0f, 0.0f, 0.0f), vec3( 0.0f,  0.0f,  1.0f), vec3(0.0f, -1.0f,  0.0f)),
    lookAt(vec3(0.0f, 0.0f, 0.0f), vec3( 0.0f,  0.0f, -1.0f), vec3(0.0f, -1.0f,  0.0f))
};
```

fragment shader 中根据立方体的局部坐标来采样环境贴图。

```c++
#version 330 core
out vec4 FragColor;
in vec3 localPos;

uniform sampler2D equirectangularMap;

const vec2 invAtan = vec2(0.1591, 0.3183);
vec2 SampleSphericalMap(vec3 v)
{
    vec2 uv = vec2(atan(v.z, v.x), asin(v.y));
    uv *= invAtan;
    uv += 0.5;
    return uv;
}

void main()
{		
    vec2 uv = SampleSphericalMap(normalize(localPos)); // make sure to normalize localPos
    vec3 color = texture(equirectangularMap, uv).rgb;
    
    FragColor = vec4(color, 1.0);
}
```

FBO 的 GL_COLOR_ATTACHMENT0 可以修改用以多次渲染，详细查看代码。也可以类似点光源渲染阴影那样一次就搞定。

搞定之后将其作为 skybox 来验证结果。

#### 立方体贴图卷积 Cubemap Convolution

我们要通过对半球面积分来计算 $L_o(p,\omega_o)$。我们不可能采样所有可能的方向，我们可用有限的采样来估计。均匀采样或者随机采样。

然而对于实时渲染来说，这个估计的计算量还是很大，所以我们对此进行预计算。

将积分变量转化为 $\theta$ 和 $\phi$ 
$$
L _ { o } \left( p , \phi _ { o } , \theta _ { o } \right) = k _ { d } \frac { c } { \pi } \int _ { \phi = 0 } ^ { 2 \pi } \int _ { \theta = 0 } ^ { \frac { 1 } { 2 } \pi } L _ { i } \left( p , \phi _ { i } , \theta _ { i } \right) \cos ( \theta ) \sin ( \theta ) d \phi d \theta
$$
均匀采样即可

> 更好的实现是 cos weighted

$$
\begin{aligned}
L _ { o } \left( p , \phi _ { o } , \theta _ { o } \right) &= k _ { d } \frac { c } { \pi } \frac { 1 } { N_1N_2 } \sum _ { \phi = 0 } ^ { N_1 } \sum _ { \theta = 0 } ^ { N_2 } \frac{L _ { i } \left( p , \phi _ { i } , \theta _ { i } \right) \cos ( \theta ) \sin ( \theta )}{\frac{1}{2\pi * 0.5\pi}}\\
&= k _ { d } c \pi \frac { 1 } { N_1N_2 } \sum _ { \phi = 0 } ^ { N_1 } \sum _ { \theta = 0 } ^ { N_2 } L _ { i } \left( p , \phi _ { i } , \theta _ { i } \right) \cos ( \theta ) \sin ( \theta )\\
\end{aligned}
$$

预计算需要计算的就是
$$
\pi \frac { 1 } { N_1N_2 } \sum _ { \phi = 0 } ^ { N_1 } \sum _ { \theta = 0 } ^ { N_2 } L _ { i } \left( p , \phi _ { i } , \theta _ { i } \right) \cos ( \theta ) \sin ( \theta )\\
$$
在使用时需要补上 $k_dc$。

渲染流程类似于生成cubemap。

#### IBL Diffuse

上边说到，为了得到 irradiance，我们还要补上  $k_d c$。c 就是 albedo，可直接获得。为了能量守恒，我们需要考虑菲涅尔项。但我们这里没有单一的半矢量来确定菲涅尔系数，因为环境光来自各个方向。我们拿法向作为半矢量方向。我们这里没有考虑进粗糙度。漫反射是间接光照，表现情况会和直接光照类似，所以可预计粗糙的表面的反射率会降低。

这里用 [Sébastien Lagarde](https://seblagarde.wordpress.com/2011/08/17/hello-world/) 给出的函数

```c++
vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness) {
    return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(1.0 - cosTheta, 5.0);
}
```

这样最终的结果为

```c++
vec3 kS = fresnelSchlickRoughness(max(dot(N, V), 0.0), F0, roughness);
vec3 kD = 1.0 - kS;
vec3 irradiance = texture(irradianceMap, N).rgb;
vec3 diffuse    = irradiance * albedo;
vec3 ambient    = (kD * diffuse) * ao;
```

### 3.2 Specular

#### 3.2.1 理论

镜面部分
$$
L _ { o } \left( p , \omega _ { o } \right) = \int _ { \Omega } f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) L _ { i } \left( p , \omega _ { i } \right) n \cdot \omega _ { i } d \omega _ { i }
$$
这个式子与之前最大的不同就是它依赖于 $w_o$。这样就不能像之前那样预计算了。

> 这里有问题。
>
> 实际上，确定好 $\omega_o$ 后，这个积分就可以算出来了，并不是像原文说的那样要预计算一个同时依赖于 $\omega_o$ 和 $\omega_i$ 的结果。问题就在于这个 BRDF 还有粗糙度，金属性，albedo这三个参数。
>
> 没法预计算，除非参数全固定。

Epic Games 的 split sum approximation 将积分分成了两部分
$$
L _ { o } \left( p , \omega _ { o } \right) = \int _ { \Omega } L _ { i } \left( p , \omega _ { i } \right) d \omega _ { i } * \int _ { \Omega } f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) n \cdot \omega _ { i } d \omega _ { i }
$$
第一部分是预过滤环境贴图 pre-filtered environment map，类似于之前的 irradiance map，但这次要考虑粗糙度。随着粗糙度等级上升，我们就用更分散的样本来积分环境贴图，用以产生更模糊的反射（通过根据 GGX 重要性采样，这个分散性就蕴含在其中了）。

> 示例
>
> 5 级预过滤环境贴图如下
>
> ![Pre-convoluted environment map over 5 roughness levels for PBR](https://learnopengl.com/img/pbr/ibl_prefilter_map.png)

根据 Cook-Torrance BRDF 的 NDF 来重要性采样生成样本方向。但是预计算时我们没有视线方向，Epic Games 假设视线方向同于法向。

> 这句话较难理解，可以参考 https://zero-radiance.github.io/post/split-sum/
>
> 简单来说就是
> $$
> L _ { o } \left( p , \omega _ { o } \right) \approx L_c(\text{roughness}) \int _ { \Omega } f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) n \cdot \omega _ { i } d \omega _ { i }
> $$
> 关键就在这个 $L_c$ 的估计上
>
> 用蒙特卡洛估计
> $$
> L_c\approx\frac{\sum L G}{\sum G}
> $$
> 而 Epic Games 将 G 设为 $l\cdot n$ 

第二部分，关键在这个 BRDF 上，有变量 $\omega_o$（由 $\theta$ 和 $\phi$ 确定），roughness，metallic，albedo。一个贴图只能有两个变量，所以我们应该转变下这个方程，使得预计算的结果只依赖于两个变量。因为 BRDF 函数是各向同性的，所以 $\omega_o$ 的 $\phi$ 不关键，只需考虑 $\theta$，为了效率，改为 $\cos\theta$。metallic 和 albedo 出现于 F 的计算中，而 F 以乘积项的形式出现在被积函数中。因此我们考虑将其分离出来
$$
\begin{aligned}
\int _ { \Omega } f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) n \cdot \omega _ { i } d \omega _ { i } 

&= \int _ { \Omega } f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) \frac { F \left( \omega _ { o } , h \right) } { F \left( \omega _ { o } , h \right) } n \cdot \omega _ { i } d \omega _ { i }\\

&= \int _ { \Omega } \frac { f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) } { F \left( \omega _ { o } , h \right) } \left( F _ { 0 } + \left( 1 - F _ { 0 } \right) \left( 1 - \omega _ { o } \cdot h \right) ^ { 5 } \right) n \cdot \omega _ { i } d \omega _ { i }\\

&=F_0 \int _ { \Omega } \frac { f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) } { F \left( \omega _ { o } , h \right) }(1-(1-\omega_o\cdot h)^5) n \cdot \omega_i d\omega_i + \int _ { \Omega } \frac { f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) } { F \left( \omega _ { o } , h \right) }(1-\omega_o\cdot h)^5 n \cdot \omega_i d\omega_i\\
\end{aligned}
$$
$f_r$ 含有 $F$，可以约去。这两部分积分只依赖于两个参数，分别是 $\cos\theta$ 和 roughness。所以可做成一个 2D 的 lookup texture LUT，称为 BRDF integration map。

> 这里的 BRDF integration map，变量只有 BRDF 中的 NDF 和 G。一组 NDF 和 G 就决定着一个 BRDF integration map。
>
> 这种贴图不依赖于 metallic, albedo，只要 BRDF 不变就能直接复用。

#### 3.2.2 预过滤环境贴图

这是一个多级的 cubemap，使用 `glGenerateMipmap(GL_TEXTURE_CUBE_MAP)` 简单分配所需内存。

要保证 minification filter 为 `GL)LINEAR_MIPMAP_LINEAR` 来进行三线性插值

```c++
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_MIN_FILTER, GL_LINEAR_MIPMAP_LINEAR); 
```

之前我们均匀采样，这对于镜面反射效率很低。

##### 蒙特卡洛积分和重要性采样

##### 低差异序列

使用 Hammersley Sequence 来进行准蒙特卡洛采样。

实现代码

```c++
float RadicalInverse_VdC(uint bits) 
{
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}
// ----------------------------------------------------------------------------
vec2 Hammersley(uint i, uint N)
{
    return vec2(float(i)/float(N), RadicalInverse_VdC(i));
}  
```

可以得到 N 个样本。

> 如果不支持位操作，可以用下边的版本
>
> ```c++
> float VanDerCorpus(uint n, uint base)
> {
>     float invBase = 1.0 / float(base);
>     float denom   = 1.0;
>     float result  = 0.0;
> 
>     for(uint i = 0u; i < 32u; ++i)
>     {
>         if(n > 0u)
>         {
>             denom   = mod(float(n), 2.0);
>             result += denom * invBase;
>             invBase = invBase / 2.0;
>             n       = uint(float(n) / 2.0);
>         }
>     }
> 
>     return result;
> }
> // ----------------------------------------------------------------------------
> vec2 HammersleyNoBitOps(uint i, uint N)
> {
>     return vec2(float(i)/float(N), VanDerCorpus(i, 2u));
> }
> ```

##### GGX 重要性采样

概率相关的推导可查看 [C08. Reflection Models](https://github.com/Ubpa/Note/blob/master/CG/PBRT/notes/C08.md#842-%E5%BE%AE%E9%9D%A2%E5%85%83%E5%88%86%E5%B8%83%E5%87%BD%E6%95%B0-microfacet-distribution-functions)。

```c++
vec3 ImportanceSampleGGX(vec2 Xi, vec3 N, float roughness)
{
    float a = roughness*roughness;
	
    float phi = 2.0 * PI * Xi.x;
    float cos2Theta = (1.0 - Xi.y) / (1.0 + (a*a - 1.0) * Xi.y);
    float cosTheta = sqrt(cos2Theta);
    float sinTheta = sqrt(1.0 - cos2Theta);
	
    // from spherical coordinates to cartesian coordinates
    vec3 H;
    H.x = cos(phi) * sinTheta;
    H.y = sin(phi) * sinTheta;
    H.z = cosTheta;
	
    // from tangent-space vector to world-space sample vector
    vec3 up        = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent   = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);
	
    vec3 sampleVec = tangent * H.x + bitangent * H.y + N * H.z;
    return normalize(sampleVec);
}
```

##### 采样实现

```c++
#version 330 core
out vec4 FragColor;
in vec3 localPos;

uniform samplerCube environmentMap;
uniform float roughness;

const float PI = 3.14159265359;

float RadicalInverse_VdC(uint bits);
vec2 Hammersley(uint i, uint N);
vec3 ImportanceSampleGGX(vec2 Xi, vec3 N, float roughness);
  
void main()
{		
    vec3 N = normalize(localPos);    
    vec3 R = N;
    vec3 V = R;

    const uint SAMPLE_COUNT = 1024u;
    float totalWeight = 0.0;   
    vec3 prefilteredColor = vec3(0.0);     
    for(uint i = 0u; i < SAMPLE_COUNT; ++i)
    {
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        vec3 H  = ImportanceSampleGGX(Xi, N, roughness);
        vec3 L  = normalize(2.0 * dot(V, H) * H - V);

        float NdotL = max(dot(N, L), 0.0);
        if(NdotL > 0.0)
        {
            prefilteredColor += texture(environmentMap, L).rgb * NdotL;
            totalWeight      += NdotL;
        }
    }
    prefilteredColor = prefilteredColor / totalWeight;

    FragColor = vec4(prefilteredColor, 1.0);
}  
```

这里还有一个关键点，L 通过 $\omega_i\cdot n$ 来加权求和，靠近 n 的贡献大，而不是简单的求均值。

> 这个加权应该是考虑到原积分中的 $\cos\theta$。
>
> 其实这个根本就不是蒙特卡洛积分，应该是函数值除以采样概率，然后求均值。
> $$
> \frac{1}{N}\sum_i^N \frac{f(x_i)}{p(x_i)}\to\frac{\sum_i^N f(x_i)w(x_i)}{\sum_i^N{\omega(x_i)}}
> $$
> 比较奇怪，感觉要查查出处
>
> ---
>
> [Real Shading in Unreal Engine 4](http://blog.selfshadow.com/publications/s2013-shading-course/karis/s2013_pbs_epic_notes_v2.pdf) 提到
>
> *As shown in the code below, we have found weighting by $cos θ_{l_k}$ achieves better results. This weighting is not present in Equation 7, which is left in a simpler form.*
>
> 看来是实践经验
>
> ---
>
> 3.2.1 已经给出了正确的理解

##### 渲染流程

预过滤环境贴图的每一级的渲染流程类似于 irradiance map。每个级渲染一次。

```c++
prefilterShader.use();
prefilterShader.setInt("environmentMap", 0);
prefilterShader.setMat4("projection", captureProjection);
glActiveTexture(GL_TEXTURE0);
glBindTexture(GL_TEXTURE_CUBE_MAP, envCubemap);

glBindFramebuffer(GL_FRAMEBUFFER, captureFBO);
unsigned int maxMipLevels = 5;
for (unsigned int mip = 0; mip < maxMipLevels; ++mip)
{
    // reisze framebuffer according to mip-level size.
    unsigned int mipWidth  = 128 * std::pow(0.5, mip);
    unsigned int mipHeight = 128 * std::pow(0.5, mip);
    glBindRenderbuffer(GL_RENDERBUFFER, captureRBO);
    glRenderbufferStorage(GL_RENDERBUFFER, GL_DEPTH_COMPONENT24, mipWidth, mipHeight);
    glViewport(0, 0, mipWidth, mipHeight);

    float roughness = (float)mip / (float)(maxMipLevels - 1);
    prefilterShader.setFloat("roughness", roughness);
    for (unsigned int i = 0; i < 6; ++i)
    {
        prefilterShader.setMat4("view", captureViews[i]);
        // 注意设置 mip
        glFramebufferTexture2D(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, 
                               GL_TEXTURE_CUBE_MAP_POSITIVE_X + i, prefilterMap, mip);

        glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT);
        renderCube();
    }
}
glBindFramebuffer(GL_FRAMEBUFFER, 0);   
```

#### 3.2.3 预过滤卷积 artifacts

##### 高粗糙度立方体贴图接缝

![Visible cubemap seams in the pre-filter map.](https://learnopengl.com/img/pbr/ibl_prefilter_seams.png)

OpenGL 没有在 cubemap face 间插值，可调用如下接口开启此插值

```c++
glEnable(GL_TEXTURE_CUBE_MAP_SEAMLESS);
```

##### 预过滤卷积的亮点

![Visible dots on high frequency HDR maps in the deeper mip LOD levels of a pre-filter map.](https://learnopengl.com/img/pbr/ibl_prefilter_dots.png)

由于高频率细节和高光反射中光强的剧烈变化，对高光反射进行卷积需要大量的样本才能很好地表示 HDR 环境反射的剧烈变化。我们已经采集了大量的样本，但在某些环境中，在一些较粗糙的 mip 级别上，这可能还不够，在这种情况下，我们将开始看到亮区周围出现了星点图案。

> 每个像素都使用了相同的随机数，这样就会产生了 artifacts
>
> 消去的方法有两种
>
> - jitter
> - cubemap filtering，如果方向对应的概率低，则用更低 mip 级的环境贴图来采样

根据 [Chetan Jags](https://chetanjags.wordpress.com/2015/08/26/image-based-lighting/) 描述，我们可以根据概率在其他的 mip 级采样环境贴图来

```c++
float D   = DistributionGGX(NdotH, roughness);
float pdf = (D * NdotH / (4.0 * HdotV)) + 0.0001; 

float resolution = 512.0; // resolution of source cubemap (per face)
float saTexel  = 4.0 * PI / (6.0 * resolution * resolution);
float saSample = 1.0 / (float(SAMPLE_COUNT) * pdf + 0.0001);

float mipLevel = roughness == 0.0 ? 0.0 : 0.5 * log2(saSample / saTexel); 
```

为此，我们需要让环境贴图可以三线性插值

```c++
glBindTexture(GL_TEXTURE_CUBE_MAP, envCubemap);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_MIN_FILTER, GL_LINEAR_MIPMAP_LINEAR); 
```

还要让 OpenGL 生成相应的 mipmaps

```c++
// convert HDR equirectangular environment map to cubemap equivalent
[...]
// then generate mipmaps
glBindTexture(GL_TEXTURE_CUBE_MAP, envCubemap);
glGenerateMipmap(GL_TEXTURE_CUBE_MAP);
```

#### 3.2.4 BRDF 预计算

我们要计算的公式是
$$
\begin{aligned}
\int _ { \Omega } f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) n \cdot \omega _ { i } d \omega _ { i } 

&=F_0 \int _ { \Omega } \frac { f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) } { F \left( \omega _ { o } , h \right) }(1-(1-\omega_o\cdot h)^5) n \cdot \omega_i d\omega_i + \int _ { \Omega } \frac { f _ { r } \left( p , \omega _ { i } , \omega _ { o } \right) } { F \left( \omega _ { o } , h \right) }(1-\omega_o\cdot h)^5 n \cdot \omega_i d\omega_i\\

&=F_0 A + B
\end{aligned}
$$
所以我们最终要的是一个贴图 $f:(U,V)\to(A,B)$。U 是 $\cos\theta$，V 是 `roughness`。

用 GPU 来计算，我们可以渲染一个 2D 的平面，得到的纹理坐标就是 $\cos\theta$ 和 `roughness`。

用蒙特卡洛积分来计算 $A$ 和 $B$，他们都根据 BRDF 来进行重要性采样，则概率为
$$
p(\omega_o)=\frac{D(\omega_h)(\omega_h\cdot n)}{4(\omega_o\cdot \omega_h)}
$$
则 A 的蒙特卡洛积分公式为
$$
\begin{aligned}
A\approx&\frac{1}{N}\sum^N\frac{\frac{D(\omega_h)G(\omega_o,\omega_i)}{4(\omega_o\cdot\omega_h)(\omega_i\cdot\omega_h)}(1-(1-\omega_o\cdot\omega_h)^5)(\omega_o\cdot n)}{p(\omega_o)}\\
=&\frac{1}{N}\sum^N\frac{G(\omega_o,\omega_i)(\omega_o\cdot n)(1-(1-\omega_o\cdot\omega_h)^5)}{(\omega_i\cdot\omega_h)(\omega_h\cdot n)}
\end{aligned}
$$
B 也类似

函数 G 中的 k 应该使用 $k_\text{IBL}=\alpha^2/2$。

#### IBL 反射

预计算的贴图

```c++
uniform samplerCube prefilterMap;
uniform sampler2D   brdfLUT;
```

获取 $L_c$ 

```c++
vec3 R = reflect(-V, N);   
const float MAX_REFLECTION_LOD = 4.0;
// 用 R 来采样
vec3 prefilteredColor = textureLod(prefilterMap, R,  roughness * MAX_REFLECTION_LOD).rgb; 
```

镜面项

```c++
vec3 F        = FresnelSchlickRoughness(max(dot(N, V), 0.0), F0, roughness);
vec2 envBRDF  = texture(brdfLUT, vec2(max(dot(N, V), 0.0), roughness)).rg;
vec3 specular = prefilteredColor * (F * envBRDF.x + envBRDF.y);
```

> 合并
>
> ```c++
> vec3 F = FresnelSchlickRoughness(max(dot(N, V), 0.0), F0, roughness);
> 
> vec3 kS = F;
> vec3 kD = 1.0 - kS;
> kD *= 1.0 - metallic;	  
>   
> vec3 irradiance = texture(irradianceMap, N).rgb;
> vec3 diffuse    = irradiance * albedo;
>   
> const float MAX_REFLECTION_LOD = 4.0;
> vec3 prefilteredColor = textureLod(prefilterMap, R,  roughness * MAX_REFLECTION_LOD).rgb;   
> vec2 envBRDF  = texture(brdfLUT, vec2(max(dot(N, V), 0.0), roughness)).rg;
> vec3 specular = prefilteredColor * (F * envBRDF.x + envBRDF.y);
>   
> vec3 ambient = (kD * diffuse + specular) * ao; 
> ```

## 参考文献

[^1]: Walter B, Marschner S R, Li H, et al. **Microfacet models for refraction through rough surfaces**[C]//Proceedings of the 18th Eurographics conference on Rendering Techniques. Eurographics Association, 2007: 195-206.
[^2]: Schlick C . **An Inexpensive BRDF Model for Physically-based Rendering**[J]. Computer Graphics Forum, 1994, 13(3):233-246.

