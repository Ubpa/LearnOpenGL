#version 330 core
out vec4 FragColor;

in VS_OUT {
    vec3 FragPos;
    vec2 TexCoords;
    vec3 TangentLightPos;
    vec3 TangentViewPos;
    vec3 TangentFragPos;
} fs_in;

uniform sampler2D diffuseMap;
uniform sampler2D normalMap;
uniform sampler2D depthMap;

uniform float heightScale;

uniform int mode;

vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir, int mode)
{ 
	if(mode==1){
		float height =  texture(depthMap, texCoords).r;     
		return texCoords - viewDir.xy / viewDir.z * (height * heightScale);    
	}else if(mode == 2){
		// number of depth layers
		const float minLayers = 8;
		const float maxLayers = 32;
		float numLayers = mix(maxLayers, minLayers, abs(dot(vec3(0.0, 0.0, 1.0), viewDir)));  
		// calculate the size of each layer
		float layerDepth = 1.0 / numLayers;
		// depth of current layer
		float currentLayerDepth = 0.0;
		// the amount to shift the texture coordinates per layer (from vector P)
		vec2 P = viewDir.xy / viewDir.z * heightScale;
		vec2 deltaTexCoords = P / numLayers;
  
		// get initial values
		vec2  currentTexCoords     = texCoords;
		float currentDepthMapValue = texture(depthMap, currentTexCoords).r;
      
		while(currentLayerDepth < currentDepthMapValue)
		{
			// shift texture coordinates along direction of P
			currentTexCoords -= deltaTexCoords;
			// get depthmap value at current texture coordinates
			currentDepthMapValue = texture(depthMap, currentTexCoords).r;  
			// get depth of next layer
			currentLayerDepth += layerDepth;  
		}
    
		return currentTexCoords;
	}else if(mode ==3){
		// number of depth layers
		const float minLayers = 8;
		const float maxLayers = 32;
		float numLayers = mix(maxLayers, minLayers, abs(dot(vec3(0.0, 0.0, 1.0), viewDir)));  
		// calculate the size of each layer
		float layerDepth = 1.0 / numLayers;
		// depth of current layer
		float currentLayerDepth = 0.0;
		// the amount to shift the texture coordinates per layer (from vector P)
		vec2 P = viewDir.xy / viewDir.z * heightScale;
		vec2 deltaTexCoords = P / numLayers;
  
		// get initial values
		vec2  currentTexCoords     = texCoords;
		float currentDepthMapValue = texture(depthMap, currentTexCoords).r;
		float lastDepthMapValue = 999999;
		
		while(currentLayerDepth < currentDepthMapValue)
		{
			// shift texture coordinates along direction of P
			currentTexCoords -= deltaTexCoords;
			// get depthmap value at current texture coordinates
			lastDepthMapValue = currentDepthMapValue;
			currentDepthMapValue = texture(depthMap, currentTexCoords).r;  
			// get depth of next layer
			currentLayerDepth += layerDepth;
		}
		float d1 = currentLayerDepth - currentDepthMapValue;
		float d2 = lastDepthMapValue - currentLayerDepth + layerDepth;
		currentTexCoords = mix(currentTexCoords, currentTexCoords + deltaTexCoords, d1 / (d1+d2));
    
		return currentTexCoords;
	}
	else{
		return texCoords;
	}
}

void main()
{           
    // offset texture coordinates with Parallax Mapping
    vec3 viewDir = normalize(fs_in.TangentViewPos - fs_in.TangentFragPos);
    vec2 texCoords = fs_in.TexCoords;
    
    texCoords = ParallaxMapping(fs_in.TexCoords,  viewDir, mode);       
    if(texCoords.x > 1.0 || texCoords.y > 1.0 || texCoords.x < 0.0 || texCoords.y < 0.0)
        discard;

    // obtain normal from normal map
    vec3 normal = texture(normalMap, texCoords).rgb;
    normal = normalize(normal * 2.0 - 1.0);   
   
    // get diffuse color
    vec3 color = texture(diffuseMap, texCoords).rgb;
    // ambient
    vec3 ambient = 0.1 * color;
    // diffuse
    vec3 lightDir = normalize(fs_in.TangentLightPos - fs_in.TangentFragPos);
    float diff = max(dot(lightDir, normal), 0.0);
    vec3 diffuse = diff * color;
    // specular    
    vec3 reflectDir = reflect(-lightDir, normal);
    vec3 halfwayDir = normalize(lightDir + viewDir);  
    float spec = pow(max(dot(normal, halfwayDir), 0.0), 32.0);

    vec3 specular = vec3(0.2) * spec;
    FragColor = vec4(ambient + diffuse + specular, 1.0);
}